//
// Adapted[by thingverse user padorange - Pierre-Alain Dorange] 
// from Adrew Plumb/ClothBot original version
// just change internal parameters to made dimension control easier
// a single parameter : logo diameter (millimeters)
//
// Then later addapted to implicitcad, and minor style changes.
//
// oshw_logo_2D(diameter) generate a 2D logo with diameter requested
// just have to extrude to get a 3D version, then add it to your objects
//
// cc-by-sa, pierre-alain dorange, july 2012
//  - changed by Jasper den Ouden

module gear_tooth_2d(d) 
{   polygon([[0.0,10.0*d/72.0], [0.5*d,d/15.0], 
             [0.5*d,-d/15.0], [0.0,-10.0*d/72.0]] );
}

module oshw_logo_2d(d=10.0)
{   rotate(-135)
    {   difference() 
        {   union() 
            {   circle(r=14.0*d/36.0,$fn=20);
                for( a= [45: 45: 8*45] ) rotate(a) gear_tooth_2d(d);
            }
            circle(r=10.0*d/72.0,$fn=20);
            intersection() 
            {   rotate(-20) square(size=[10.0*d/18.0,10.0*d/18.0]);
                rotate(20)  square(size=[10.0*d/18.0,10.0*d/18.0]);
            }
        }
    }
}
//module openhw(d=10.0){ oshw_logo_2d(d); }

// usage : oshw_logo_2d(diameter)
linear_extrude(height=2) oshw_logo_2d(25);
